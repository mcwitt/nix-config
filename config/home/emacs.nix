{ pkgs, ... }:
let
  fetchEmacsOverlay =
    { owner ? "nix-community", repo ? "emacs-overlay", rev, sha256 }:
    builtins.fetchTarball {
      url = "https://github.com/${owner}/${repo}/archive/${rev}.tar.gz";
      sha256 = sha256;
    };
in {

  nixpkgs.overlays = [
    # NOTE: using `pkgs.fetchFromGitHub` here results in infinite recursion;
    # hence the definition of `fetchEmacsOverlay`
    (import (fetchEmacsOverlay {
      rev = "4b89b7d5476ccf8ccdd31abb9d2d18267488e26a";
      sha256 = "1vni79i5pvkil12iafmf65q1chrkm6cv3xcy9kvpry59nrlxw2hx";
    }))
  ];

  # NOTE: (2020-03-12) use this instead of `programs.emacs.package = emacsEnv`
  # because the latter is not compatible with packages generated by
  # `emacsWithPackages`. See https://github.com/NixOS/nixpkgs/issues/66706
  home.packages = [ pkgs.emacsEnv ];

  home.file.".emacs.d" = {
    source = "${pkgs.mcwitt-dotfiles}/emacs.d/";
    recursive = true;
  };
}
